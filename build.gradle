plugins {
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '0.12.0'
  id 'groovy'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}
jar {
  manifest {
    attributes 'Implementation-Version': project.version
  }
}
repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  implementation "io.gatling:gatling-enterprise-plugin-commons:1.13.4"
  implementation "io.gatling:gatling-shared-cli:0.0.6"
  constraints {
    implementation('com.fasterxml.jackson.core:jackson-databind') {
      version {
        strictly '2.14.3'
      }
      because 'some gradle versions have bug with multi-jdk jar, introduced in jackson-databind 2.15.' +
        'cf. https://github.com/gradle/gradle/issues/24390'
    }
  }

  testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
    exclude module: 'groovy-all'
  }
  testImplementation 'org.spockframework:spock-junit4:2.3-groovy-3.0'
  testImplementation('com.github.stefanbirkner:system-rules:1.19.0') {
    exclude module: 'junit-dep'
  }
  testImplementation "junit:junit:4.13.2"
  testImplementation "commons-io:commons-io:2.11.0"
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showStandardStreams = false
  }
  useJUnitPlatform()
}

gradlePlugin {
  plugins {
    gatlingPlugin {
      id = "io.gatling.gradle"
      implementationClass = 'io.gatling.gradle.GatlingPlugin'
      displayName = "Gatling plugin for Gradle"
      description = "Gatling plugin for Gradle"
    }
  }
}

pluginBundle {
  website = 'https://github.com/gatling/gatling-gradle-plugin'
  vcsUrl = 'https://github.com/gatling/gatling-gradle-plugin'
  tags = ['gatling', 'load test', 'stress test', 'performance test', 'java', 'scala', 'kotlin']
}

validatePlugins.failOnWarning = false
