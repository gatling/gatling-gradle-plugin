plugins {
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version "2.0.0"
  id 'groovy'
}

group = "gradle.plugin.io.gatling.gradle"

compileJava {
  options.release = 11
}

compileGroovy {
  options.release = 11
}

java {
  toolchain {
    // highest Java version supported by the current Gradle range we support (see GatlingFuncSpec)
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

jar {
  manifest {
    attributes 'Implementation-Version': project.version
  }
}
repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  implementation "io.gatling:gatling-enterprise-plugin-commons:1.20.4"
  implementation "io.gatling:gatling-shared-cli:0.0.6"

  testImplementation('org.spockframework:spock-core:2.3-groovy-4.0') {
    exclude module: 'groovy'
  }
  testImplementation 'org.spockframework:spock-junit4:2.3-groovy-4.0'
  testImplementation('com.github.stefanbirkner:system-rules:1.19.0') {
    exclude module: 'junit-dep'
  }
  testImplementation "org.junit.jupiter:junit-jupiter:5.12.2"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.12.2"
  testImplementation "commons-io:commons-io:2.19.0"
}

test {
  testLogging {
    events  = ["passed", "skipped", "failed"]
    exceptionFormat = "full"
    showStandardStreams = false
  }
  useJUnitPlatform()
  // required for Spock, see https://docs.gradle.org/current/userguide/upgrading_version_7.html#remove_test_add_opens
  jvmArgs = ["--add-opens=java.base/java.util=ALL-UNNAMED"]
}

gradlePlugin {
  website.set("https://github.com/gatling/gatling-gradle-plugin")
  vcsUrl.set("https://github.com/gatling/gatling-gradle-plugin")
  plugins {
    create("gatlingPlugin") {
      id = "io.gatling.gradle"
      implementationClass = "io.gatling.gradle.GatlingPlugin"
      displayName = "Gatling plugin for Gradle"
      description = "Gatling plugin for Gradle"
      tags.addAll("gatling", "load test", "stress test", "performance test", "java", "scala", "kotlin")
    }
  }
}

validatePlugins.failOnWarning = false
